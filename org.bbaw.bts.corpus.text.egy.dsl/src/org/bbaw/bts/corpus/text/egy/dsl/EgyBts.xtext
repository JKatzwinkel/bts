grammar org.bbaw.bts.corpus.text.egy.dsl.EgyBts //with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate egyBts "http://www.bbaw.org/bts/corpus/text/egy/dsl/EgyBts"

TextContent:
	{TextContent}
	(items+=TextItem (items+=TextItem)*);


TextItem:
	Sentence;

// sentence	
Sentence:
	'ยง' (items+=SentenceItem (' ' items+=SentenceItem)*) 'ยง';

SentenceItem:
	Word | AbstractMarker  | Ambivalence
;

AbstractMarker:
	Marker | VersMarker
;

Ambivalence:
	'%' (cases+=Case ('; ' cases+=Case)*) '%'
;

Case:
	'case ' name=EGYSTRING ': ' (items+=SentenceItem (' ' items+=SentenceItem)*) 
;

VersMarker:
	VersbreakMarker | VersFrontierMarker
;

VersFrontierMarker:
	'@mv'
;

VersbreakMarker:
	'@v'
;

Marker:
	'#' type=EGYSTRING name=EGYSTRING '#'
;

// word
Word:
	{Word}
	
	( wChar+=WordMiddle)*	
;

WordMiddle:
//	{WordMiddle}
	Interfix | Brackets | Chars 
;

// Chars...
Chars:
	name=EGYSTRING
;

terminal EGYSTRING: (!(' '|'\n'|'\t' | 'ยง' | '#'|'%'|';'
	// interfixes
	|'.'|','|':'|'-'|'~'|'\u2261'|'\u22ee'
	// brackets
	|'('|')'|'<'|'>'|'?'
	|'{' | '}'
	|'\u2E22' | '\u2E23'
	
))*;

// textual criticism interfixes
Interfix:
	InterfixFlexion | InterfixLexical | InterfixSuffixPronomLexical |InterfixPrefixNonLexical |InterfixPrefixLexical
	| InterfixCompountWords | InterfixConnectionSyllabicGroup
;

InterfixLexical:
	{InterfixLexical}
	','
;

InterfixFlexion:
	{InterfixFlexion}
	'.'
;

InterfixSuffixPronomLexical:
	{InterfixSuffixPronomLexical}
	'\u2261'
;

InterfixPrefixNonLexical:
	{InterfixPrefixNonLexical}
	':'
;

InterfixPrefixLexical:
	{InterfixPrefixLexical}
	'\u22ee'
;

InterfixCompountWords:
	{InterfixCompountWords}
	'-'
;

InterfixConnectionSyllabicGroup:
	{InterfixConnectionSyllabicGroup}
	'~'
;

// textual criticism brackets
Brackets:
	Rasur | AncientExpanded | RestorationOverRasur |
	Expanded  | DisputableReading | Emendation | Lacuna | Deletion | ExpandedColumn  
	| PartialDestruction | Destruction
;

Expanded:
	{Expanded}
	'(' wChar+=NoExpanded* ')'
;

NoExpanded:
	Interfix | DisputableReading | Chars
;

Emendation:
	{Emendation}
	'<' wChar+=NoEmendation* '>'
	;

NoEmendation:
	Interfix | Expanded | DisputableReading | Chars
;

DisputableReading:
	{DisputableReading}
	'?' wChar+=NoDisputableReading* '?'
;

NoDisputableReading:
	Interfix | Expanded | Chars
;

Lacuna:
	{Lacuna}
	'[' wChar+=NoLacuna* ']'
;

NoLacuna:
	Interfix | Expanded | Chars
;

Deletion:
	{Deletion}
	'{' wChar+=NoDeletion '}'
;

NoDeletion:
	Interfix  | Chars
;

ExpandedColumn:
	{ExpandedColumn}
	'<<' wChar+=NoExpandedColumn '>>'
;

NoExpandedColumn:
	Interfix  | Chars
;

Rasur:
	{Rasur}
	'{{' wChar+=NoRasur '}}'
;

NoRasur:
	Interfix  | Chars
;

AncientExpanded:
	{AncientExpanded}
	'((' wChar+=NoAncientExpanded '))'
;

NoAncientExpanded:
	Chars
;

RestorationOverRasur:
	{RestorationOverRasur}
	'[[' wChar+=NoRestorationOverRasur ']]'
;

NoRestorationOverRasur:
	Chars
;

PartialDestruction:
	{PartialDestruction}
	'\u2E22' wChar+=NoPartialDestruction '\u2E23'
;

NoPartialDestruction:
	Chars
;

Destruction:
	{Destruction}
	'--' comment=EGYSTRING '--'
;

